buildscript {
	dependencies {
		// Entity 필드가 컬렉션 타입이 아닌 경우에 FetchType.LAZY 적용하기
		classpath 'org.hibernate:hibernate-gradle-plugin:5.6.7.Final'
	}
}

plugins {
	id 'org.springframework.boot' version '2.6.6' // fix for CVE-2022-22965
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'war'
	id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10' // Querydsl
}

// Entity 필드가 컬렉션 타입이 아닌 경우에 FetchType.LAZY 적용하기
apply plugin: 'org.hibernate.orm'

group = 'io.ezsurvey'
version = '1.0'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	
	// Spring Session & Redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.session:spring-session-data-redis'

	// JSP
	implementation 'javax.servlet:jstl'
	implementation 'org.apache.tomcat.embed:tomcat-embed-jasper'
	
	// Tiles
	implementation 'org.apache.tiles:tiles-jsp:3.0.7'
	
	// Oracle Cloud Database
	/*
	implementation 'com.oracle.database.security:oraclepki'
	implementation 'com.oracle.database.security:osdt_core'
	implementation 'com.oracle.database.security:osdt_cert'
	*/
	
	// Gradle에서 OJDBC 관련 NoSuchFileException이 지속적으로 발생하여 로컬 jar로 대체
	implementation files('libs/ojdbc11.jar')
	implementation files('libs/oraclepki.jar')
	implementation files('libs/osdt_cert.jar')
	implementation files('libs/osdt_core.jar')
	
	// Querydsl
	implementation 'com.querydsl:querydsl-core'
	implementation 'com.querydsl:querydsl-jpa'
	
	// SQL 쿼리의 바인딩 파라미터를 보기 편하게 로깅
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.8.0'
	
	// StringUtils
	implementation 'org.apache.commons:commons-lang3'
	
	// HTML 태그 일괄 제거하기 위해
	implementation 'org.jsoup:jsoup:1.14.3'
	
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	// runtimeOnly 'com.oracle.database.jdbc:ojdbc8'
	annotationProcessor 'org.projectlombok:lombok'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

// Querydsl
def querydslDir = 'src/main/generated'
querydsl {
	library = 'com.querydsl:querydsl-apt' // 생략시 빌드 실패
	jpa = true
	querydslSourcesDir = querydslDir
}

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java', querydslDir]
		}
	}
}

compileQuerydsl {
	options.annotationProcessorPath = configurations.querydsl
}

configurations {
	querydsl.extendsFrom compileClasspath
}

// Entity 필드가 컬렉션 타입이 아닌 경우에 FetchType.LAZY 적용하기
hibernate {
	enhance {
		enableLazyInitialization = true
		enableDirtyTracking = false
		enableAssociationManagement = false
		enableExtendedEnhancement = false
	}
}

dependencies {
    compile 'org.hibernate:hibernate-core:5.6.7.Final'
}